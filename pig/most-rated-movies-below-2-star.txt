ratings = LOAD '/user/maria_dev/ml-100k/u.data' AS (userID:int, movieID:int, rating:int, ratingTime:int);
metadata = LOAD '/user/maria_dev/ml-100k/u.item' USING PigStorage('|')
	AS (movieID:int, movieTitle:chararray, releaseDate:chararray, videoRelaese:chararray, imdblink:chararray);
   
nameLookup = FOREACH metadata GENERATE movieID, movieTitle,
	ToUnixTime(ToDate(releaseDate, 'dd-MMM-yyyy')) AS releaseTime;

ratingsByMovie = GROUP ratings BY movieID;
avgRatings = FOREACH ratingsByMovie GENERATE group AS movieID, AVG(ratings.rating) AS avgRating, COUNT(ratings.rating) AS numRating;

below2Rating = FILTER avgRatings BY avgRating < 2.0;
below2WithData = JOIN below2Rating BY movieID, nameLookup BY movieID;

below2Movies = FOREACH below2WithData GENERATE nameLookup::movieTitle, below2Rating::avgRating AS avgRating, below2Rating::numRating AS numRating;
finalResults = ORDER below2Movies BY numRating DESC;
DUMP finalResults;